buildscript {
    repositories {
        jcenter()
        mavenCentral()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:2.3.0'
        classpath 'net.ltgt.gradle:gradle-errorprone-plugin:0.0.8'
    }

}

plugins {
    id "com.jfrog.bintray" version "1.4"
    id "org.sonarqube" version "2.2"
}

apply plugin: 'com.android.library'
apply plugin: 'net.ltgt.errorprone'
apply plugin: 'com.jfrog.bintray'

allprojects {
    group = 'util.android.viewpagerindicator'
    version = '1.1.13'
}

repositories {
    jcenter()
    mavenCentral()
    maven {
        url  "http://dl.bintray.com/indieboyjeff/maven"
    }

}

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.2"

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 25
        versionCode 2
        versionName version
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    compile 'util.android.textviews:library:2.0@aar'
    compile 'com.android.support:appcompat-v7:25.3.0'
}

def siteUrl = 'https://github.com/iNdieboyjeff/ViewPagerIndicatorExtensions'      // Homepage URL of the library
def gitUrl = 'https://github.com/iNdieboyjeff/ViewPagerIndicatorExtensions.git'   // Git repository URL

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}


artifacts {
    archives sourcesJar
}

Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())

bintray {
    user = properties.getProperty("bintray.user")
    key = properties.getProperty("bintray.apikey")

    configurations = ['archives']
    pkg {
        repo = "maven"
        desc = 'ViewPagerIndicator with extensions. Works with Android API 23.'
        name = "viewpagerindicator"
        publicDownloadNumbers = true
        websiteUrl = siteUrl
        vcsUrl = gitUrl
        licenses = ["Apache-2.0"]
        publish = true
        version {
            name = '1.1.13'
            desc = 'ViewPagerIndicator Extensions'
        }
    }
}
